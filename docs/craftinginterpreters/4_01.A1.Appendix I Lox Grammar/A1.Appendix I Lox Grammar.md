# 后记

你已经读完这本书了！有两份补充材料可能对您有所帮助：

- [附录 I](http://craftinginterpreters.com/appendix-i.html)包含一个完整的 Lox 语法，全部集中在一个地方。

- [附录 II](http://craftinginterpreters.com/appendix-ii.html)显示了 我们用于 jlox[的 AST 生成器生成](http://craftinginterpreters.com/representing-code.html#metaprogramming-the-trees)的 Java 类

# 附录一

这是 Lox 的完整语法。介绍该语言每一部分的章节都包括那里的语法规则，但这将它们全部收集到一个地方。

## A1.1 句法文法

句法语法用于将标记的线性序列解析为嵌套语法树结构。它从匹配整个 Lox 程序（或单个 REPL 条目）的第一条规则开始。

```
program        → declaration* EOF ;
```

### A1.1.1 声明

程序是一系列声明，它们是绑定新标识符或任何其他语句类型的语句。

```
declaration    → classDecl
               | funDecl
               | varDecl
               | statement ;

classDecl      → "class" IDENTIFIER ( "<" IDENTIFIER )?
                 "{" function* "}" ;
funDecl        → "fun" function ;
varDecl        → "var" IDENTIFIER ( "=" expression )? ";" ;
```

### A1.1.2 语句

其余语句规则会产生副作用，但不会引入绑定。

```
statement      → exprStmt
               | forStmt
               | ifStmt
               | printStmt
               | returnStmt
               | whileStmt
               | block ;

exprStmt       → expression ";" ;
forStmt        → "for" "(" ( varDecl | exprStmt | ";" )
                           expression? ";"
                           expression? ")" statement ;
ifStmt         → "if" "(" expression ")" statement
                 ( "else" statement )? ;
printStmt      → "print" expression ";" ;
returnStmt     → "return" expression? ";" ;
whileStmt      → "while" "(" expression ")" statement ;
block          → "{" declaration* "}" ;
```

请注意，这`block`是一条语句规则，但在其他一些规则中也用作非终结符，例如函数体。

### A1.1.3 表达式

表达式产生值。Lox 有许多具有不同优先级的一元和二元运算符。一些语言语法不直接编码优先关系并在别处指定。在这里，我们为每个优先级别使用单独的规则以使其明确。

```
expression     → assignment ;

assignment     → ( call "." )? IDENTIFIER "=" assignment
               | logic_or ;

logic_or       → logic_and ( "or" logic_and )* ;
logic_and      → equality ( "and" equality )* ;
equality       → comparison ( ( "!=" | "==" ) comparison )* ;
comparison     → term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term           → factor ( ( "-" | "+" ) factor )* ;
factor         → unary ( ( "/" | "*" ) unary )* ;

unary          → ( "!" | "-" ) unary | call ;
call           → primary ( "(" arguments? ")" | "." IDENTIFIER )* ;
primary        → "true" | "false" | "nil" | "this"
               | NUMBER | STRING | IDENTIFIER | "(" expression ")"
               | "super" "." IDENTIFIER ;
```

### A1.1.4 效用规则

为了使上述规则更清晰一些，一些语法被拆分成几个可重用的辅助规则。

```
function       → IDENTIFIER "(" parameters? ")" block ;
parameters     → IDENTIFIER ( "," IDENTIFIER )* ;
arguments      → expression ( "," expression )* ;
```

## A1.2 词汇语法

扫描器使用词汇语法将字符分组为标记。语法是[上下文无关](https://en.wikipedia.org/wiki/Context-free_grammar)的，词法语法是[规则的](https://en.wikipedia.org/wiki/Regular_grammar)——注意没有递归规则。

```
NUMBER         → DIGIT+ ( "." DIGIT+ )? ;
STRING         → "\"" <any char except "\"">* "\"" ;
IDENTIFIER     → ALPHA ( ALPHA | DIGIT )* ;
ALPHA          → "a" ... "z" | "A" ... "Z" | "_" ;
DIGIT          → "0" ... "9" ;
```